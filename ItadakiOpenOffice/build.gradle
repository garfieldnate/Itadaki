apply plugin: 'java'

version = '1.2'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.openoffice', name: 'unoil', version: '4.1.2'
    compile group: 'org.openoffice', name: 'juh', version: '4.1.2'
    compile project(':FastTextPane')
    compile project(':GoSen')
    compile project(':ItadakiDictionary')
    compile project(':ItadakiFurigana')
    compile project(':JISX0213')
    compile project(':Seashell')

    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "RegistrationClassName": "org.itadaki.openoffice.ComponentProvider"
        )
    }
}

task unoPackage(type: Zip, dependsOn: build) {
    archiveName = "itadaki-${version}.uno.pkg"
    from configurations.runtime.allArtifacts.files
    from jar.outputs.files
    from(projectDir) {
        include([
                "COPYING.LGPL",
                "README.md"
        ])
    }
    from("unopackage") {
        include([
                "description.xml",
                "description.txt",
                "*.xcu"
        ])
        filter {line -> line.replace("{VERSION}", version.toString())}
    }
    from("unopackage") {
        include([
                "manifest.xml"])
        into "META-INF"
    }
}

task install(type: Exec, dependsOn: unoPackage) {
    commandLine 'unopkg', 'remove', unoPackage.outputs.files.asPath
    commandLine 'unopkg', 'add', unoPackage.outputs.files.asPath
}

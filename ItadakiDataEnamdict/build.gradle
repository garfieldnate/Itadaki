apply plugin: 'base'
apply from: './properties.gradle'
apply from: '../scripts/dic_utils.gradle'

dependencies {
    project(':Seashell')
}

task downloadEnamdict {
    doLast {
        downloadFile(project.properties['dic.url'], project.properties['dic.archive.file'])
    }
}

task extractEnamdict(dependsOn: 'downloadEnamdict') {
    doLast{
        extractTextArchive(project.properties['dic.archive.file'], project.properties['dic.unpacked.file'])
    }
}

task compile(dependsOn: ['extractEnamdict', ':Seashell:jar']) {
    doLast{
        compileEdict(project.properties['dic.unpacked.file'])
    }
}

task unoPackage(type: Zip, dependsOn: 'compile') {
    archiveName = project.properties['archiveFileName']
    from project.properties['dic.unpacked.file']
    from getIndexFileFor(project.properties['dic.unpacked.file'])
    from (projectDir) {
        include ([
                "itadaki-data.properties",
                "LICENSE",
                "README"
        ])
    }
    from ("unopackage") {
        include(["description.xml"])
        filter {line -> line.replace("YYYYMMDD", project.properties['dic.version'])}
    }
    from ("unopackage") {
        include([
                "manifest.xml"])
        into "META-INF"
    }
}

build.dependsOn('unoPackage')
